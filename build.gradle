
buildscript {
    apply from: 'version.gradle'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {

            android {
                compileSdkVersion rootProject.ext.target_version
                buildToolsVersion "30.0.2"

                defaultConfig {
                    minSdkVersion rootProject.ext.minsdk_version
                    targetSdkVersion rootProject.ext.target_version
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                buildFeatures {
                    dataBinding true
                }
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }
                kotlinOptions {
                    jvmTarget = '1.8'
                }
            }

            dependencies {
                // JUNIT
                testImplementation 'junit:junit:4.13.2'
                androidTestImplementation 'androidx.test.ext:junit:1.1.3'
                // ESPRESSO - UI TESTING
                androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
                // AndroidJUnitRunner and JUnit Rules
                androidTestImplementation 'androidx.test:runner:1.4.0'
                androidTestImplementation 'androidx.test:rules:1.4.0'

                // MOCKITO - UNIT TESTING
                testImplementation "org.mockito:mockito-core:$mockito_version"
                testImplementation "org.mockito:mockito-inline:$mockito_version"
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}